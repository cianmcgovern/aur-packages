# Contributor Cian Mc Govern <cian@cianmcgovern.com>
# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=decklink
_basever=10.0
_mever=3.3
pkgver=${_basever}a1
pkgrel=1
pkgdesc="Drivers for Blackmagic Design DeckLink, Intensity or Multibridge video editing cards"
arch=('i686' 'x86_64')
url="http://www.blackmagic-design.com/products/"
license=('custom')
depends=('linux-headers' 'libxml2' 'mesa' 'qt4' 'libpng12')
options=('!strip')
install='decklink.install'
source=("http://software.blackmagicdesign.com/DesktopVideo/Blackmagic_Desktop_Video_Linux_${_basever}.tar.gz"
        'decklink.conf')
md5sums=('2eef4fb0d62e9788d40adcf90838b1b8'
         '4f2033439305c4ce8e9d50f55d5c46d8')

prepare() {
  _dv="DesktopVideo_${_basever}/other/${CARCH}"
  _me="MediaExpress_${_mever}/other/${CARCH}"
  [ "$CARCH" = "i686" ] && _arch='i386' && _debarch='i386'
  [ "$CARCH" = "x86_64" ] && _arch='x86_64' && _debarch='amd64'
  tar zxf "${srcdir}/${_dv}/desktopvideo-${_basever}a7-${_arch}.tar.gz"
  tar zxf "${srcdir}/${_me}/mediaexpress-${_mever}a1-${CARCH}.tar.gz"
}

build() {
  cd "${srcdir}/desktopvideo-${_basever}a7-${CARCH}/usr/src/blackmagic-${_basever}a7"
  make
  cp -dpr --no-preserve=ownership "${srcdir}/desktopvideo-${_basever}a7-${CARCH}/usr/src/blackmagic-${_basever}a7/blackmagic.ko" "${srcdir}"
  make clean

  cd "${srcdir}/desktopvideo-${_basever}a7-${CARCH}/usr/src/blackmagic-io-${_basever}a7"
  make
  cp -dpr --no-preserve=ownership "${srcdir}/desktopvideo-${_basever}a7-${CARCH}/usr/src/blackmagic-io-${_basever}a7/blackmagic-io.ko" "${srcdir}"
  make clean
}

package() {
  # license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s /usr/share/doc/desktopvideo/License.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # automatic module loading
  install -D -m 0644 "${srcdir}/decklink.conf" "$pkgdir/etc/modules-load.d/decklink.conf"
  install -D -m 0644 "${srcdir}/blackmagic.ko" "${pkgdir}/usr/lib/modules/extramodules-3.13-ARCH/blackmagic.ko"
  install -D -m 0644 "${srcdir}/blackmagic-io.ko" "${pkgdir}/usr/lib/modules/extramodules-3.13-ARCH/blackmagic-io.ko"

  cp -dpr --no-preserve=ownership "${srcdir}/desktopvideo-${_basever}a7-${CARCH}/etc" "${pkgdir}"
  cp -dpr --no-preserve=ownership "${srcdir}/desktopvideo-${_basever}a7-${CARCH}/usr" "${pkgdir}"

  cp -dpr --no-preserve=ownership "${srcdir}/mediaexpress-${_mever}a1-${CARCH}/usr" "${pkgdir}"
}

# vim:set ts=2 sw=2 et:
